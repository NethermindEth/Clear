import Mathlib.Data.Finmap
import Mathlib.Init.Data.List.Lemmas

import Clear.Ast
import Clear.State
import Clear.PrimOps
import Clear.EVMState
import Clear.SizeLemmas

namespace Clear.Interpreter

open Clear Ast State PrimOps SizeLemmas

-- ============================================================================
--  INTERPRETER
-- ============================================================================

def head' : State Ã— List Literal â†’ State Ã— Literal
  | (s, rets) => (s, List.head! rets)

def cons' (arg : Literal) : State Ã— List Literal â†’ State Ã— List Literal
  | (s, args) => (s, arg :: args)

def reverse' : State Ã— List Literal â†’ State Ã— List Literal
  | (s, args) => (s, args.reverse)

def multifill' (vars : List Identifier) : State Ã— List Literal â†’ State
  | (s, rets) => s.multifill vars rets

mutual
  def evalTail (fuel : Nat) (args : List Expr) : State Ã— Literal â†’ State Ã— List Literal
    | (s, arg) => cons' arg (evalArgs fuel args s)
  termination_by _ => 1 + fuel + sizeOf args

  /--
    `evalArgs` evaluates a list of arguments.
  -/
  def evalArgs (fuel : Nat) (args : List Expr) (s : State) : State Ã— List Literal :=
    match args with
      | [] => (s, [])
      | arg :: args =>
        evalTail fuel args (eval fuel arg s)
  termination_by fuel + sizeOf args
  decreasing_by
    all_goals simp_wf; try simp_arith
    try apply Expr.zero_lt_sizeOf

  /--
    `call` executes a call of a user-defined function.
  -/
  def call (fuel : Nat) (args : List Literal) (f : FunctionDefinition) (s : State) : State Ã— List Literal :=
    let sâ‚ := ðŸ‘Œ initcall f.params args s
    let sâ‚‚ := exec fuel (.Block f.body) sâ‚
    let sâ‚ƒ := reviveJump sâ‚‚ |>.overwrite? s |>.setStore s
    (sâ‚ƒ, List.map sâ‚‚.lookup! f.rets)
  termination_by fuel + sizeOf f
  decreasing_by
    all_goals simp_wf
    simp_arith
    apply FunctionDefinition.sizeOf_body_succ_lt_sizeOf

  -- Safe to call `List.head!` on return values, because the compiler throws an
  -- error when coarity is > 0 in (1) and when coarity is > 1 in all other
  -- cases.

  def evalPrimCall (prim : PrimOp) : State Ã— List Literal â†’ State Ã— Literal
    | (s, args) => head' (primCall s prim args)

  def evalCall (fuel : Nat) (f : FunctionDefinition) : State Ã— List Literal â†’ State Ã— Literal
    | (s, args) => head' (call fuel args f s)
  termination_by _ => 1 + fuel + sizeOf f

  def execPrimCall (prim : PrimOp) (vars : List Identifier) : State Ã— List Literal â†’ State
    | (s, args) => multifill' vars (primCall s prim args)

  def execCall (fuel : Nat) (f : FunctionDefinition) (vars : List Identifier) : State Ã— List Literal â†’ State
    | (s, args) => multifill' vars (call fuel args f s)
  termination_by _ => 1 + fuel + sizeOf f

  /--
    `execSwitchCases` executes each case of a `switch` statement.
  -/
  def execSwitchCases (fuel : Nat) (s : State) : List (Literal Ã— List Stmt) â†’ List (Literal Ã— State)
    | [] => []
    | ((val, stmts) :: cases') => (val, exec fuel (.Block stmts) s) :: execSwitchCases fuel s cases'
  termination_by x => fuel + sizeOf x

  /--
    `eval` evaluates an expression.

    - calls evaluated here are assumed to have coarity 1
  -/
  def eval (fuel : Nat) (expr : Expr) (s : State) : State Ã— Literal :=
    match expr with

      -- We hit these two cases (`PrimCall` and `Call`) when evaluating:
      --
      --  1. f()                 (expression statements)
      --  2. g(f())              (calls in function arguments)
      --  3. if f() {...}        (if conditions)
      --  4. for {...} f() ...   (for conditions)
      --  5. switch f() ...      (switch conditions)

      | .PrimCall prim args => evalPrimCall prim (reverse' (evalArgs fuel args.reverse s))
      | .Call f args        => evalCall fuel f (reverse' (evalArgs fuel args.reverse s))
      | .Var id             => (s, s[id]!!)
      | .Lit val            => (s, val)
  termination_by fuel + sizeOf expr
  decreasing_by
    all_goals
    simp_wf
    try simp_arith
    try apply Expr.zero_lt_sizeOf_List

  /--
    `exec` executs a single statement.
  -/
  def exec (fuel : Nat) (stmt : Stmt) (s : State) : State :=
    match stmt with
      | .Block [] => s
      | .Block (stmt :: stmts) =>
        let sâ‚ := exec fuel stmt s
        exec fuel (.Block stmts) sâ‚

      | .Let vars => List.foldr (Î» var s â†¦ s.insert var 0) s vars

      | .LetEq var rhs =>
        let (s, val) := eval fuel rhs s
        s.insert var val

      | .LetCall vars f args => execCall fuel f vars (reverse' (evalArgs fuel args.reverse s))

      | .LetPrimCall vars prim args => execPrimCall prim vars (reverse' (evalArgs fuel args.reverse s))

      | .Assign var rhs =>
        let (s, x) := eval fuel rhs s
        s.insert var x

      | .AssignCall vars f args => execCall fuel f vars (reverse' (evalArgs fuel args.reverse s))

      | .AssignPrimCall vars prim args => execPrimCall prim vars (reverse' (evalArgs fuel args.reverse s))

      | .If cond body =>
        let (s, cond) := eval fuel cond s
        if cond â‰  0 then exec fuel (.Block body) s else s

      -- "Expressions that are also statements (i.e. at the block level) have
      -- to evaluate to zero values."
      --
      -- (https://docs.soliditylang.org/en/latest/yul.html#restrictions-on-the-grammar)
      --
      -- Thus, we cannot have literals or variables on the RHS.
      | .ExprStmtCall f args => execCall fuel f [] (reverse' (evalArgs fuel args.reverse s))
      | .ExprStmtPrimCall prim args => execPrimCall prim [] (reverse' (evalArgs fuel args.reverse s))

      | .Switch cond cases' default' =>

        let (sâ‚, cond) := eval fuel cond s
        let branches := execSwitchCases fuel sâ‚ cases'
        let sâ‚‚ := exec fuel (.Block default') sâ‚
        List.foldr (Î» (valáµ¢, sáµ¢) s â†¦ if valáµ¢ = cond then sáµ¢ else s) sâ‚‚ branches

      -- A `Break` or `Continue` in the pre or post is a compiler error,
      -- so we assume it can't happen and don't modify the state in these
      -- cases. (https://docs.soliditylang.org/en/v0.8.23/yul.html#loops)
      | .For cond post body => loop fuel cond post body s
      | .Continue => ðŸ” s
      | .Break => ðŸ’” s
      | .Leave => ðŸšª s
  termination_by fuel + sizeOf stmt
  decreasing_by
    all_goals
    simp_wf
    try simp_arith
    try apply le_add_right
    try apply List.zero_lt_sizeOf
    try apply Expr.zero_lt_sizeOf
    try apply Expr.zero_lt_sizeOf_List

  /--
    `loop` executes a for-loop.
  -/
  def loop (fuel : Nat) (cond : Expr) (post body : List Stmt) (s : State) : State :=
    match fuel with
      | 0 => diverge s
      | 1 => diverge s
      | fuel + 1 + 1 =>
        let (sâ‚, x) := eval fuel cond (ðŸ‘Œs)
        if x = 0
          then sâ‚âœï¸âŸ¦sâŸ§?
          else
            let sâ‚‚ := exec fuel (.Block body) sâ‚
            match sâ‚‚ with
              | .OutOfFuel                      => sâ‚‚âœï¸âŸ¦sâŸ§?
              | .Checkpoint (.Break _ _)      => ðŸ§Ÿsâ‚‚âœï¸âŸ¦sâŸ§?
              | .Checkpoint (.Leave _ _)      => sâ‚‚âœï¸âŸ¦sâŸ§?
              | .Checkpoint (.Continue _ _)
              | _ =>
                let sâ‚ƒ := exec fuel (.Block post) (ðŸ§Ÿ sâ‚‚)
                let sâ‚„ := sâ‚ƒâœï¸âŸ¦sâŸ§?
                let sâ‚… := exec fuel (.For cond post body) sâ‚„
                let sâ‚† := sâ‚…âœï¸âŸ¦sâŸ§?
                sâ‚†
  termination_by fuel + sizeOf cond + sizeOf post + sizeOf body
  decreasing_by
    all_goals
    simp_wf
    simp_arith

end

notation "ðŸ„" => exec
notation "ðŸŒ¸" => eval

section EXPERIMENTAL

inductive EvalOutcome where
  | O (Ïƒ : EState) (v : Literal)
  | OutOfFuel
  | Malformed

inductive EvalLOutcome where
  | O (Ïƒ : EState) (l : List Literal)
  | OutOfFuel
  | Malformed

inductive ExecOutcome where
  | O (Ïƒ : EState)
  | B (Ïƒ : EState)
  | C (Ïƒ : EState)
  | L (Ïƒ : EState)
  | OutOfFuel
  | Malformed

namespace EvalOutcome

def mapD { T : Type } (o : EvalOutcome) (defOOF : T) (defM : T) (f : EState -> Literal -> T) : T :=
  match o with
  | .O Ïƒ v => f Ïƒ v
  | .OutOfFuel => defOOF
  | .Malformed => defM

def map := mapD (defOOF := EvalOutcome.OutOfFuel) (defM := .Malformed)
def mapExecOutcome := mapD (defOOF := ExecOutcome.OutOfFuel) (defM := .Malformed)
def mapEvalLOutcome := mapD (defOOF := EvalLOutcome.OutOfFuel) (defM := .Malformed)

end EvalOutcome

namespace EvalLOutcome

def mapD { T : Type } (o : EvalLOutcome) (defOOF : T) (defM : T) (f : EState -> List Literal -> T) : T :=
  match o with
  | .O Ïƒ vs => f Ïƒ vs
  | .OutOfFuel => defOOF
  | .Malformed => defM

def map := mapD (defOOF := EvalLOutcome.OutOfFuel) (defM := .Malformed)
def mapEvalOutcome := mapD (defOOF := EvalOutcome.OutOfFuel) (defM := .Malformed)
def mapExecOutcome := mapD (defOOF := ExecOutcome.OutOfFuel) (defM := .Malformed)

end EvalLOutcome

def lookup (Ïƒ : EState) (var : Identifier) : EvalOutcome :=
  ((Ïƒ.eLookup var).map (EvalOutcome.O Ïƒ Â·)).getD .Malformed

def lookupL (Ïƒ : EState) (vars : List Identifier) : EvalLOutcome :=
    match vars with
    | [] => .O Ïƒ []
    | var :: vars =>
      (lookup Ïƒ var).mapEvalLOutcome
      (Î» Ïƒ v => (lookupL Ïƒ vars).map (Î» Ïƒ vs => .O Ïƒ (v :: vs)))

mutual

  def eEval (fuel : â„•) (Ïƒ : EState) (e : Expr) : EvalOutcome :=
    match e with
    | .Lit v  => .O Ïƒ v
    | .Var id => lookup Ïƒ id
    | .Call f args =>
        -- expression function calls must return precisely one value
        if (f.rets.length = 1) then
          -- Yul semantics evaluates arguments right-to-left
          (eEvalL fuel Ïƒ args.reverse).mapEvalOutcome
          (eCalle fuel f Â· Â·.reverse)
        else
          .Malformed
    -- Not implemented
    | _ => .Malformed
    termination_by (fuel + sizeOf e, 0)
    decreasing_by
      all_goals
      simp_wf
      apply Prod.Lex.left
      simp_arith

  def eEvalL (fuel : â„•) (Ïƒ : EState) (es : List Expr) : EvalLOutcome :=
    match es with
    | [] => .O Ïƒ []
    | e :: es =>
      (eEval fuel Ïƒ e).mapEvalLOutcome
      (Î» Ïƒ v => (eEvalL fuel Ïƒ es).map (Î» Ïƒ vs => .O Ïƒ (v :: vs)))
    termination_by (fuel + sizeOf es, 0)
    decreasing_by
      all_goals
      simp_wf
      apply Prod.Lex.left
      simp_arith

  def eCall
    { T : Type }
    (fuel : Nat)
    (funDef : FunctionDefinition)
    (Ïƒ : EState)
    (args : List Literal)
    (defOOF : T)
    (defM : T)
    (f : EState -> T) : T :=
      let Ïƒâ‚ := Ïƒ.eInitCall funDef.params args
      let ob := eExec fuel Ïƒâ‚ (.Block funDef.body)
      match ob with
      -- Ok and Leave outcomes are ok for function calls
      | .O Ïƒâ‚‚ | .L Ïƒâ‚‚ => f Ïƒâ‚‚
      | .OutOfFuel => defOOF
      -- Break, Continue, and Malformed outcomes are malformed
      | .B .. | .C .. | .Malformed => defM
    termination_by (fuel + sizeOf funDef, 0)
    decreasing_by
      all_goals simp_wf
      simp_arith
      apply Prod.Lex.left
      simp_arith
      apply FunctionDefinition.sizeOf_body_succ_lt_sizeOf

  def eCalle
    (fuel : Nat)
    (f : FunctionDefinition)
    (Ïƒ : EState)
    (args : List Literal) : EvalOutcome :=
      let store := Ïƒ.store
      eCall fuel f Ïƒ args .OutOfFuel .Malformed
      (Î» Ïƒâ‚ =>
        -- expression function calls must return precisely one value
        match f.rets with
        | [] => .Malformed
        | [ ret ] =>
          (lookup Ïƒâ‚ ret).map
          (Î» Ïƒâ‚‚ v =>
            let Ïƒâ‚ƒ := Ïƒâ‚‚.eSetStore store
            .O Ïƒâ‚ƒ v)
        | _ :: _ => .Malformed)
    termination_by (fuel + sizeOf f, 1)

  def eCallx
    (fuel : Nat)
    (f : FunctionDefinition)
    (Ïƒ : EState)
    (vars : List Identifier)
    (args : List Literal) : ExecOutcome :=
      let store := Ïƒ.store
      eCall fuel f Ïƒ args .OutOfFuel .Malformed
      (Î» Ïƒâ‚ =>
        (lookupL Ïƒâ‚ f.rets).mapExecOutcome
        (Î» Ïƒâ‚‚ rets =>
          let Ïƒâ‚ƒ := Ïƒâ‚‚.eSetStore store
          .O <| Ïƒâ‚ƒ.eMultiFill vars rets))
    termination_by (fuel + sizeOf f, 1)

  def eExec (fuel : â„•) (Ïƒ : EState) (s : Stmt) : ExecOutcome :=
    match s with
      -- break, continue, and leave simply set the appropriate outcome type
      | .Break => .B Ïƒ
      | .Continue => .C Ïƒ
      | .Leave => .L Ïƒ

      | .Block [] => .O Ïƒ
      | .Block (s :: stmts) =>
          let os := eExec fuel Ïƒ s
          -- sequence shortcuts everything except ok
          match os with
          | .O Ïƒ => eExec fuel Ïƒ (.Block stmts)
          | .B .. | .C .. | .L .. | .OutOfFuel | .Malformed => os

      | .Let vars =>
        .O <| vars.foldl (init := Ïƒ) (EState.eInsert Â· Â· 0)

      | .LetEq var e
      | .Assign var e =>
        (eEval fuel Ïƒ e).mapExecOutcome
        (Î» Ïƒ v => .O (EState.eInsert Ïƒ var v))

      | .If cond body =>
        (eEval fuel Ïƒ cond).mapExecOutcome
        (Î» Ïƒ v => if v â‰  0 then eExec fuel Ïƒ (.Block body) else .O Ïƒ)

      | .Switch cond cases dflt =>
        (eEval fuel Ïƒ cond).mapExecOutcome
        (Î» Ïƒ case =>
          let stmts := cases.foldl (init := dflt)
                       (Î» s (caseáµ¢, stmtsáµ¢) â†¦ if caseáµ¢ = case then stmtsáµ¢ else s)
          eExec fuel Ïƒ (.Block stmts))

      | .LetCall    vars f args
      | .AssignCall vars f args =>
        if vars.length = f.rets.length then
          (eEvalL fuel Ïƒ args.reverse).mapExecOutcome
          (Î» Ïƒ vals => eCallx fuel f Ïƒ vars vals.reverse)
        else
          .Malformed

      | .ExprStmtCall f args =>
        if f.rets.length = 0 then
          (eEvalL fuel Ïƒ args.reverse).mapExecOutcome
          (Î» Ïƒ vals => eCallx fuel f Ïƒ [] vals.reverse)
        else
          .Malformed

      | .For cond post body =>
        match fuel with
          -- out of fuel
          | 0 => .OutOfFuel
          -- there still is fuel
          | fuel + 1 =>
            (eEval fuel Ïƒ cond).mapExecOutcome
            (Î» Ïƒ v =>
              if v = 0
                -- loop condition false, exit loop
                then .O Ïƒ
                else
                  -- loop condition true, execute body
                  let ob := eExec fuel Ïƒ (.Block body)
                  match ob with
                  -- ok and continue proceed to the next iteration
                  | .O Ïƒ | .C Ïƒ =>
                    -- execute post
                    let op := eExec fuel Ïƒ (.Block post)
                    match op with
                    -- execute next iteration
                    | .O Ïƒ => eExec fuel Ïƒ (.For cond post body)
                    -- out of fuel propagates
                    | .OutOfFuel => .OutOfFuel
                    -- everything else (break, continue, leave, malformed) is malformed
                    | .B .. | .C .. | .L .. | .Malformed => .Malformed
                  -- break changes to ok and shortcuts
                  | .B Ïƒ => .O Ïƒ
                  -- everything else shortcuts
                  | .L .. | .OutOfFuel | .Malformed => ob)
      -- Not implemented
      | _ => .Malformed
    termination_by (fuel + sizeOf s, 0)
    decreasing_by
      all_goals (simp_wf; try simp_arith)
      all_goals (apply Prod.Lex.left; simp_arith)
      rcases cond <;> simp_arith

end

end EXPERIMENTAL

section

open EVMState

variable {s sâ‚€ sâ‚ : State}
         {arg expr rhs cond : Expr}
         {args : List Expr}
         {evm : EVM}
         {store : VarStore}
         {stmt : Stmt}
         {stmts pre body post rest : List Stmt}
         {fuel m n k : â„•}
         {var var' fname : String}
         {x : Literal}
         {xs : List Literal}
         {f : FunctionDefinition}
         {prim : PrimOp}

-- ============================================================================
--  TRAVERSE LEMMAS
-- ============================================================================

section
unseal exec

/-
  Traversing an empty list is the identity on states.
-/
@[simp]
lemma nil : exec fuel (.Block []) s = s := by rfl

/--
  Traversing a nonempty list is the same traversing the tail from the state yielded from executing the head.
-/
lemma cons : exec fuel (.Block (stmt :: stmts)) s = exec fuel (.Block stmts) (exec fuel stmt s) := by
  conv_lhs => unfold exec

end

-- ============================================================================
--  EVAL LEMMAS
-- ============================================================================

section
unseal eval

/--
  Evaluating a literal gives you back that literal and the state you started in.
-/
lemma Lit' : eval fuel (.Lit x) s = (s, x) := by rfl

/--
  Evaluating a variable does a varstore lookup.
-/
lemma Var' : eval fuel (.Var var) s = (s, s[var]!!) := by rfl

end

/--
  A call in an expression.
-/
lemma Call' : eval fuel (.Call f args) s = evalCall fuel f (reverse' (evalArgs fuel args.reverse s)) := by unfold eval; rfl

/--
  Evaluating an EVM builtin evaluates the arguments, calls the builtin on
the resulting literals, and then returns the resulting state and the first
literal return value.
-/
lemma PrimCall' : eval fuel (.PrimCall prim args) s = evalPrimCall prim (reverse' (evalArgs fuel args.reverse s)) := by unfold eval; rfl

-- ============================================================================
--  HELPER FUNCTION LEMMAS
-- ============================================================================

/--
  Executing a user-defined function with the `call` interpreter helper
  function loads all the arguments and return variables into the varstore,
  traverses the body, restores the saved checkpoint to the top-level.
-/
lemma call_def : call fuel xs f s =
  let sâ‚ := ðŸ‘Œ initcall f.params xs s
  let sâ‚‚ := exec fuel (.Block f.body) sâ‚
  let sâ‚ƒ := reviveJump sâ‚‚ |>.overwrite? s |>.setStore s
  (sâ‚ƒ, List.map sâ‚‚.lookup! f.rets) := by unfold call; rfl

@[simp]
lemma evalTail_nil : evalTail fuel [] (s, x) = (s, [x]) := by
  conv_lhs => unfold evalTail cons' evalArgs

@[simp]
lemma evalTail_cons : evalTail fuel (arg :: args) (s, x) =
  match evalTail fuel args (ðŸŒ¸ fuel arg s) with
    | (s, args) => (s, x :: args) := by conv_lhs => unfold evalTail cons' evalArgs

end

end Clear.Interpreter
